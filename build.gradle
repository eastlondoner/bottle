
allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.7

    repositories {
        mavenCentral()
    }
}

subprojects {
    dependencies {
        //compile 'org.apache.hadoop:hadoop-mapreduce:2.5.1'
        compile 'org.apache.hadoop:hadoop-common:2.5.1'
        compile 'org.apache.hadoop:hadoop-mapreduce-client-core:2.5.1'

        testCompile 'junit:junit:4.11+'
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}
test.finalizedBy(testReport)


dependencies {
   compile project(':wordcount')
   compile project(':pubmedcount')
   compile project(':pubmedgenerate')
}


//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': project.name + ' Example',
                    'Implementation-Version': '1',
                    'Built-By': System.getProperty('user.name'),
                    'Built-Date': new Date(),
                    'Built-JDK': System.getProperty('java.version'),
                    'Main-Class': 'com.geneix.bottle.MapReduceRunner'
        }

    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    baseName = project.name + '-all'
    destinationDir = file('jars/')

    with jar
}